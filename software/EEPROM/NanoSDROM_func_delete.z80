; INPUT:
; - DE: points a null terminated string to a filename to delete
; OUTPUT
; - C:
;    - $00: OK
;    - $F1: Cannot delete file
;    - $F2: timeout during communicating with Nano

DELETE_HANDLER:      
            .BLOCK  delete 
            LD      C,(IX+0) 
            LD      A,SDISK_NANO_CMD_DELETE 
            OUT     (C),A ; DELETE command sent
            CALL    WAIT_100us 
            IN      A,(C) ; let's drop the first response
PATH_SEND_LOOP:      
            CALL    WAIT_100us 
            LD      A,(DE) 
            OUT     (C),A 
            OR      A 
            JR      z,SEND_PATH_CLOSE 
            INC     DE 
            JR      PATH_SEND_LOOP 
SEND_PATH_CLOSE:     
            LD      DE,5000 
WAIT_FOR_DELETE_RESPONSE:  
            CALL    WAIT_1ms 
            IN      A,(C) 
            CP      RETVAL_DELETE_OK 
            LD      B,$00 
            JR      z,DELETE_EXIT 
            CP      RETVAL_DELETE_FAILED 
            LD      B,$F1 
            JR      z,DELETE_EXIT 
            DEC     DE 
            LD      A,D 
            OR      E 
            JR      nz,WAIT_FOR_DELETE_RESPONSE 
            LD      B,$F2 
DELETE_EXIT:         
            LD      A,B 
            RET      
            .ENDBLOCK  

